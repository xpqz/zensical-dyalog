---
search:
  boost: 2
---

# File Copy

```apl
R←X ⎕FCOPY Y
```
[Key to notation](../key-to-notation.md)

## Access Code: 4609

`Y` must be a simple integer scalar or 1 or 2-element vector containing the file tie number and optional passnumber. The file need not be tied exclusively.

`X` is a character vector containing the name of a new file to be copied to. If no file extension is supplied, the first extension specified by the   **CFEXT** parameter will be added. See [ CFEXT](../../windows-installation-and-configuration-guide/configuration-parameters/configuration-parameters.md).

`⎕FCOPY` creates a copy of the tied file specified by `Y`, named `X`.

The new file `X` will have the same  component level information, including the user number and update time as the original. The operating system file creation, modification and access times will be set to the time at which the copy occurred.

Unless otherwise specified (see File Properties below) the new file `X` will have the same file properties as the original, except that it will be a large-span file regardless of the span of the original.

The result `R` is the file tie number associated with the new file `X`.

The Access Code is 4609, which is the sum of the Access Codes for `⎕FREAD` (1), `⎕FRDCI` (512), and `⎕FRDAC` (4096).

Although the file need not be tied exclusively, the `⎕FCOPY` function does not yield the file to other APL processes while it is running, and it might take a considerable time to run in the case of a large component file.

## Example
```apl
      told←'oldfile32'⎕FTIE 0
      'S' ⎕FPROPS told
32
      tnew←'newfile64' ⎕FCOPY told
 
      'S' ⎕FPROPS tnew
64
```

If `X` specifies the name of an existing file, the operation fails with a `FILE NAME ERROR`.

This operation is atomic. If an error occurs during the copy operation (such as disk full) or if a strong interrupt is issued, the copy is aborted and the new file `X` is not created.

## Variant Options

`⎕FCOPY` sets the properties of the new file through variant options, summarised in [](#variantoptionsforfcopy). These are the file properties described in [File Properties](fprops.md). Unless a property is specified, the new file inherits it from the source, except `'S'`, which is always `64`.

Table: Variant options for `⎕FCOPY` { #variantoptionsforfcopy }

|Variant Option|Valid Values|Effect|
|---|---|---|
|`'J'`|`0`, `1`, `2`, or `3`|Sets the journaling level.|
|`'C'`|`0` or `1`|Sets the checksum level.|
|`'Z'`|`0` or `1`|Sets compression.|
|`'U'`|`0` or `1`|Sets Unicode support.|
|`'S'`|`64`|Sets the file size (span).|

The principal option is a number that sets journaling (`'J'`) and checksum (`'C'`) together:

|Principal option|Effect|
|---|---|
|`0`|sets `('J' 0) ('C' 0)`, which signals a `DOMAIN ERROR` as this combination is no longer supported|
|`1`|sets `('J' 1) ('C' 1)`|
|`2`|sets `('J' 2) ('C' 1)`|
|`3`|sets `('J' 3) ('C' 1)`|

## Examples
```apl
      newfid←'newfile' (⎕FCOPY ⍠3) 1

      'SEUJCZ' ⎕FPROPS newfid
64 0 1 3 1 0
```

Alternatively:
```apl
      JFCOPY←⎕FCOPY ⍠ 3
```

will name a variant of `⎕FCREATE` which will create component file with level 3 journaling, and checksum enabled. Then:
```apl
      newfid←'newfile' JFCOPY 1

```

!!! tip "Hints and Recommendations"
    Setting `('U' 0)` (no Unicode support) is discouraged as it might cause the copy to fail with a `TRANSLATION ERROR`. Similarly, using a Classic interpreter to `⎕FCOPY` files might result in `TRANSLATION ERROR`s.

!!! Info "Information"
    Small-span (32-bit) component files are currently read-only; Dyalog Ltd recommends using `⎕FCOPY` to convert any such files to large-span (64-bit). This ability is scheduled for removal in a future release. For information on how to identify calls to small-span component files in your existing codebase, see the [Release Notes](../../release-notes/announcements/deprecated-functionality.md).

<!-- Hidden search keywords -->
<div style="display: none;">
  ⎕FCOPY FCOPY
</div>
