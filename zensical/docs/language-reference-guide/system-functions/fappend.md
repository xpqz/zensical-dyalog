---
search:
  boost: 2
---

# File Append Component `{R}←X ⎕FAPPEND Y`

```apl
{R}←X ⎕FAPPEND Y
```
[Key to notation](../key-to-notation.md)

## Access code 8

`Y` must be a simple integer scalar or a 1 or 2 element vector containing the file tie number followed by an optional passnumber.  If the passnumber is omitted it is assumed to be zero. Subject to a few restrictions, `X` may be any array.

The [shy](../../programming-reference-guide/introduction/results.md#shy-results) result `R` is the number of the component to which `X` is written, and is 1 greater than the previously highest component number in the file, or 1 if the file is new.

## Examples
```apl
      (1000?1000) ⎕FAPPEND 1
 
      ⎕←(2 3⍴⍳6) 'Geoff' (⎕OR'FOO') ⎕FAPPEND 1
12
 
      ⎕←A B C ⎕FAPPEND¨1
13 14 15

Dump←{
    tie←⍺ ⎕FCREATE 0              ⍝ create file.
    (⎕FUNTIE tie){}⍵ ⎕FAPPEND tie ⍝ append and untie.
}
```

!!! Info "Information"
    Component files that have both journalling and checksum properties set to `0` have been deprecated, and component files with this combination of properties are read-only. Dyalog Ltd recommends using `⎕FPROPS` to convert any such files to have different properties. For information on how to identify component files that have both journalling and checksum properties set to `0` in your existing codebase, see the [Release Notes](../../release-notes/announcements/deprecated-functionality.md).

<!-- Hidden search keywords -->
<div style="display: none;">
  ⎕FAPPEND FAPPEND
</div>
